apply plugin: 'com.android.application'
apply plugin: 'de.mobilej.unmock'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "fr.rhaz.ipfs.sweet"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 11
        versionName "0.11"
        archivesBaseName = "sweetipfs-$versionName"
        vectorDrawables.useSupportLibrary = true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$archivesBaseName-${variant.name}.apk"
        }
        variant.assemble.doLast { assemble ->
            copy {
                from variant.outputs*.outputFile
                into file("/apk")
            }
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        // for OKIO
        warning 'InvalidPackage'
    }
}

repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "com.github.hotchemi:permissionsdispatcher:$permissiondispatcher_version"
    kapt "com.github.hotchemi:permissionsdispatcher-processor:$permissiondispatcher_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    testImplementation "junit:junit:4.12"
    testImplementation "com.squareup.assertj:assertj-android:1.2.0"
    testImplementation "com.android.support:support-annotations:$support_version"

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.22.5"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.ligi:tracedroid:1.4"
    implementation "com.google.dagger:dagger:$dagger_version"

    implementation "com.android.support:support-v13:$support_version"
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"

    implementation "com.chibatching.kotpref:kotpref:2.5.0"
    implementation "com.chibatching.kotpref:initializer:2.5.0"

    implementation 'net.steamcrafted:load-toast:1.0.12'
    implementation 'com.github.ligi:ipfs-api-kotlin:0.12'

    implementation 'com.github.ligi:KAXT:0.20'
    implementation 'com.github.ligi:KAXTUI:0.4'

    implementation 'com.github.kenglxn.QRGen:android:2.4.0'

    implementation 'com.google.android.gms:play-services-oss-licenses:16.0.0'

    kaptAndroidTest "com.google.dagger:dagger-compiler:$dagger_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}